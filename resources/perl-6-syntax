
		<p>In Perl 6, you can restrict the content of a variable container by specifying its type, for example:</p>
<pre>my Int $i;</pre>
<p>There is only one value in a scalar variable. You can extend the concept to arrays and let its element to keep only integers, as it is done in the next example:</p>
<pre>&gt; my Int @i;
[]

&gt; @i.push(42);
[42]

&gt; @i.push(&#39;Hello&#39;);
Type check failed in assignment to @i;
expected Int but got Str (&quot;Hello&quot;)
  in block &lt;unit&gt; at &lt;unknown file&gt; line 1</pre>
<p>Hashes keeps pairs, so you can specify the type of both keys and values. The syntax is not deductible from the above examples.</p>
<p>First, let us announce the type of the value:</p>
<pre class="p1"><span class="s1">my Str %s;</span></pre>
<p>Now, it is possible to have strings as values:</p>
<pre>&gt; %s&lt;Hello&gt; = &#39;World&#39;
World

&gt; %s&lt;42&gt; = &#39;Fourty-two&#39;
Fourty-two</pre>
<p>But it’s not possible to save integers:</p>
<pre>&gt; %s&lt;x&gt; = 100
Type check failed in assignment to %s;
expected Str but got Int (100)
  in block &lt;unit&gt; at &lt;unknown file&gt; line 1</pre>
<p>(By the way, notice that in the case of <tt>%s&lt;42&gt;</tt> the key is a string.)</p>
<p>To specify the type of the second dimension, namely, of the hash keys, give the type in curly braces:</p>
<pre>my %r{Rat};</pre>
<p>This variable is also referred to as <em>object hash</em>.</p>
<p>Having this, Perl expects you to have <tt>Rat</tt> keys for this variable:</p>
<pre>&gt; %r&lt;22/7&gt; = pi
3.14159265358979

&gt; %r
{22/7 =&gt; 3.14159265358979}</pre>
<p>Attempts to use integers or strings, for example, fail:</p>
<pre>&gt; %r&lt;Hello&gt; = 1
Type check failed in binding to parameter &#39;key&#39;;
expected Rat but got Str (&quot;Hello&quot;)
  in block &lt;unit&gt; at &lt;unknown file&gt; line 1

&gt; %r{23} = 32
Type check failed in binding to parameter &#39;key&#39;;
expected Rat but got Int (23)
  in block &lt;unit&gt; at &lt;unknown file&gt; line 1</pre>
<p>Finally, you can specify the types of both keys and values:</p>
<pre>my Str %m{Int};</pre>
<p>This variable can be used for translating month number to month names but not vice versa:</p>
<pre>&gt; %m{3} = &#39;March&#39;
March

&gt; %m&lt;March&gt; = 3
Type check failed in binding to parameter &#39;key&#39;;
expected Int but got Str (&quot;March&quot;)
  in block &lt;unit&gt; at &lt;unknown file&gt; line 1</pre>
<p> </p>
	

		<p>In Perl 5, I used to set timeouts using signals (or, at least, that was an easy and predictable way). In Perl 6, you can use promises. Let us see how to do that.</p>
<p>To imitate a long-running task, create an infinite loop that prints its state now and then. Here it is:</p>
<pre>for 1 .. * {
    .say if $_ %% 100_000;
}</pre>
<p>As soon as the loop gets control, it will never quit. Our task is to stop the program in a couple of seconds, so the timer should be set before the loop:</p>
<pre>Promise.in(2).then({
    exit;
});

for 1 .. * {
    .say if $_ %% 100_000;
}</pre>
<p>Here, the <tt>Promise.in</tt> method creates a promise that is automatically kept after the given number of seconds. On top of that promise, using <tt>then</tt>, we add another promise, whose code will be run after the timeout. The only statement in the body here is <tt>exit</tt> that stops the main program.</p>
<p>Run the program to see how it works:</p>
<pre>$ time perl6 timeout.pl
100000
200000
300000
. . .
3700000
3800000
3900000

real 0m2.196s
user 0m2.120s
sys 0m0.068s</pre>
<p>The program counts up to about four millions on my computer and quits in two seconds. That is exactly the behaviour we needed.</p>
<p>For comparison, here is the program in Perl 5:</p>
<pre>use v5.10;

alarm 2;
$SIG{ALRM} = sub {
    exit;
};

for (my $c = 1; ; $c++) {
    say $c unless $c % 1_000_000;
}</pre>
<p>(It manages to count up to 40 million, but that’s another story.)</p>
	

		<p>N. B. The examples below require a fresh Rakudo compiler, at least of the version 2017.09.</p>
<p>Discussing parallel computing earlier or later leads to solving race conditions. Let us look at a simple counter that is incremented by two parallel threads:</p>
<pre>my $c = 0;

await do for 1..10 {
    start {
        $c++ for 1 .. 1_000_000
    }
}

say $c;</pre>
<p>If you run the program a few times, you will immediately see that the results are very different:</p>
<pre>$ perl6 atomic-1.pl 
3141187
$ perl6 atomic-1.pl 
3211980
$ perl6 atomic-1.pl 
3174944
$ perl6 atomic-1.pl 
3271573</pre>
<p>Of course, the idea was to increase the counter by 1 million in all of the ten threads, but about ⅓ of the steps were lost. It is quite easy to understand why that happens: the parallel threads read the variable and write to it ignoring the presence of other threads and not thinking that the value can be changed in-between. Thus, some of the threads work with an outdated value of the counter.</p>
<p>Perl 6 offers a solution: atomic operations. The syntax of the language is equipped with the <em>Atom Symbol</em> (U+0x269B) ⚛ character (no idea of why it is displayed in that purple colour). Instead of <tt>$c++</tt>, you should type <tt>$c⚛++</tt>.</p>
<pre>my <strong>atomicint</strong> $c = 0;

await do for 1..10 {
    start {
        $c⚛++ for 1 .. 1_000_000
    }
}

say $c;</pre>
<p>And before thinking of the necessity to use a Unicode character, let us look at the result of the updated program:</p>
<pre>$ perl6 atomic-2.pl 
10000000</pre>
<p>This is exactly the result we wanted!</p>
<p>Notice also, that the variable is declared as a variable of the <tt>atomicint</tt> type. That is a synonym for <tt>int</tt>, which is a <a href="https://perl6.online/2018/01/15/26-native-integers-and-uint-in-perl-6/"><em>native</em> integer</a> (unlike <tt>Int</tt>, which is a data type represented by a Perl 6 class).</p>
<p>It is not possible to ask a regular value to be atomic. That attempt will be rejected by the compiler:</p>
<pre>$ perl6 -e&#39;my $c; $c⚛++&#39;
Expected a modifiable native int argument for &#39;$target&#39;
  in block  at -e line 1</pre>
<p>A few other operators can be atomic, for example, prefix and postfix increments and decrements <tt>++</tt> and <tt>--</tt>, or <tt>+=</tt> and <tt>-=</tt>. There are also atomic versions of the assignment operator <tt>=</tt> and the one for reading: ⚛ <em>(sic!)</em>.</p>
<p>If you need atomic operations in your code, you are not forced to use the ⚛ character. There exists a bunch of alternative functions that you can use instead of the operators:</p>
<pre>my atomicint $c = 1;

my $x = ⚛$c;  $x = atomic-fetch($c);
$c ⚛= $x;     atomic-assign($c, $x);
$c⚛++;        atomic-fetch-inc($c);
$c⚛--;        atomic-fetch-dec($c);
++⚛$c;        atomic-inc-fetch($c);
--⚛$c;        atomic-dec-fetch($c);
$c ⚛+= $x;    atomic-fetch-add($c,$x);

say $x; <em># 1</em>
say $c; <em># 3</em></pre>
	

		<p>When you print an object, say, as <tt>say $x</tt>, Perl 6 calls the <tt>gist</tt> method. This method is defined for all built-in types: for some of them, it calls the <tt>Str</tt> method, for some the <tt>perl</tt> method, for some types it makes the string representation somehow differently.</p>
<p>Let us see how you can use the method to create your own variant:</p>
<pre>class X {
    has $.value;

    method gist {
        &#39;[&#39; ~ $!value ~ &#39;]&#39;
    }
}

my $x = X.new(value =&gt; 42);

say $x; <em># [42]</em>
$x.say; <em># [42]</em></pre>
<p>When you call <tt>say</tt>, the program prints a number in square brackets: <tt>[42]</tt>.</p>
<p>Please notice that the interpolation inside double-quoted strings is using <tt>Str</tt>, not <tt>gist</tt>. You can see it here:</p>
<pre>say $x.Str; <em># X&lt;140586830040512&gt;</em>
say &quot;$x&quot;;   <em># X&lt;140586830040512&gt;</em></pre>
<p>If you need a custom interpolation, redefine the <tt>Str</tt> method:</p>
<pre>class X {
    has $.value;

    method gist {
        &#39;[&#39; ~ $!value ~ &#39;]&#39;
    }
    method Str {
        &#39;&quot;&#39; ~ $!value ~ &#39;&quot;&#39;
    }
}

my $x = X.new(value =&gt; 42);

say $x;     <em># [42]</em>
$x.say;     <em># [42]</em>

say $x.Str; <em># &quot;42&quot;</em>
say &quot;$x&quot;;   <em># &quot;42&quot;</em></pre>
<p>Now, we got the desired behaviour.</p>
	

		<p>Before digging into the details of the <a href="https://perl6.online/2018/02/20/62-the-eval-routine-in-perl-6-part-1/"><tt>EVAL</tt> routine</a>, we have to reveal some more information <a href="https://perl6.online/2017/12/21/the-proto-keyword/">about <tt>proto</tt>s</a> and multiple dispatch. Examine the following program:</p>
<pre><span style="color:#0000ff;">proto</span> sub f($x) {
    say &quot;proto f($x)&quot;;
}

<span style="color:#0000ff;">multi</span> sub f($x) {
    say &quot;f($x)&quot;
}

<span style="color:#0000ff;">multi</span> sub f(Int $x) {
    say &quot;f(Int $x)&quot;
}

<span style="color:#0000ff;">multi</span> sub f(Str $x) {
    say &quot;f(Str $x)&quot;
}

f(2);
f(&#39;2&#39;);
f(3);
f(&#39;3&#39;);</pre>
<p>Here, there are three multi-candidates of the function plus a function declared with the <tt>proto</tt> keyword. Earlier, we only saw such proto-functions with empty body, such as:</p>
<pre>proto sub f($x) {*}</pre>
<p>But this is not a necessity. The function can carry a regular load, as we see in the example:</p>
<pre>proto sub f($x) {
    say &quot;proto f($x)&quot;;
}</pre>
<p>Run the program:</p>
<pre>proto f(2)
proto f(2)
proto f(3)
proto f(3)</pre>
<p>All the calls were caught by the proto-candidate. Now, update it and return the <tt>{*}</tt> block for some dedicated values;</p>
<pre>proto sub f($x) {
   <span style="color:#0000ff;"> if $x.Str eq &#39;3&#39; {
        return {*}
    }</span>
    say &quot;proto f($x)&quot;;
}</pre>
<p>The <tt>if</tt> check triggers its block for the last two function calls:</p>
<pre>f(3);
f(&#39;3&#39;);</pre>
<p>In these cases, the proto-function returns <tt>{*}</tt>, which makes Perl 6 trying other candidates. As we have enough candidates for both integer and string arguments, the compiler can easily choose one of them:</p>
<pre>proto f(2)
proto f(2)
f(Int 3)
f(Str 3)</pre>
<div></div>
	

		<p>The <tt>EVAL</tt> routine in Perl 6 compiles and executes the code that it gets as an argument.  Today, we will see some potential use cases that you may try in your practice. Tomorrow, we will dig into Rakudo sources to see how it works and why it breaks sometimes.</p>
<h2>1</h2>
<p>Let us start with evaluating a simple program:</p>
<pre>EVAL(&#39;say 123&#39;);</pre>
<p>This program prints <tt>123</tt>, there’s no surprise here.</p>
<h2>2</h2>
<p>There are, though, more complicated cases. What, do you think, does the following program print?</p>
<pre>EVAL(&#39;say {456}&#39;);</pre>
<p>I guess it prints not what you expected:</p>
<pre>-&gt; ;; $_? is raw { #`(Block|140570649867712) ... }</pre>
<p>It parses the content between the curly braces as a pointy block.</p>
<h2>3</h2>
<p>What if you try double quotes?</p>
<pre>EVAL(&quot;say {789}&quot;);</pre>
<p>Now it even refuses to compile:</p>
<pre>===SORRY!=== Error while compiling eval.pl
EVAL is a very dangerous function!!! (use the MONKEY-SEE-NO-EVAL pragma to override this error,
but only if you&#39;re VERY sure your data contains no injection attacks)
at eval.pl:6
------&gt; EVAL(&quot;say {789}&quot;)⏏;</pre>
<h2>4</h2>
<p>We can fix the code by adding a few magic words:</p>
<pre>use MONKEY-SEE-NO-EVAL;

EVAL(&quot;say {789}&quot;);</pre>
<p>This time, it prints <tt>789</tt>.</p>
<h2>5</h2>
<p>The code is executed (we don’t know yet when exactly, that is the topic of tomorrow’s post), so you can make some calculations, for example:</p>
<pre>use MONKEY-SEE-NO-EVAL;

EVAL(&quot;say {7 / 8 + 9}&quot;); <em># 9.875</em></pre>
<h2>6</h2>
<p>Finally, if you try passing a code block directly, you also cannot achieve the goal, even with a blind monkey:</p>
<pre>use MONKEY-SEE-NO-EVAL;

EVAL {say 123};</pre>
<p>The error happens at runtime:</p>
<pre>Constraint type check failed in binding to parameter &#39;$code&#39;;
expected anonymous constraint to be met but got 
-&gt; ;; $_? is raw { #`...
  in block &lt;unit&gt; at eval.pl line 10</pre>
<p>This message looks cryptic, but at least we see once again that we got an anonymous pointy block passed to the function.</p>
<h2>7</h2>
<p>And before we wrap up for today, an attempt to use Perl 5 syntax:</p>
<pre>eval(&#39;say 42&#39;);</pre>
<p>There is no such function in Perl 6, and we get a standard error message:</p>
<pre>===SORRY!=== Error while compiling eval2.pl
Undeclared routine:
  eval used at line 5. Did you mean &#39;EVAL&#39;, &#39;val&#39;?</pre>
<p>It looks OK but it can be better.</p>
<p>Stay tuned, tomorrow we will try to understand how all these examples work in Rakudo.</p>
	

		<p>Today, a small excursus into the syntax. Did you know that roles in Perl 6 can have a parameter that makes them similar to generic templates in, say, C++? Here’s a small example:</p>
<pre>role R {
    has $.value;
 
    method add($b) {
        $.value + $b.value
    }

    method div($b) {
        $.value / $b.value
    }
}</pre>
<p>The <tt>R</tt> role defines an interface that has a value and two methods for arithmetical operations: <tt>add</tt> and <tt>div</tt>.</p>
<p>Now, create a class using the role, initialise two variables and use the methods to get the results:</p>
<pre>class C does R {}

my C $x = C.new(value =&gt; 10);
my C $y = C.new(value =&gt; 3);

say $x.add($y); <em># 13</em>
say $x.div($y); <em># <span class="s1">3.333333</span></em></pre>
<p>Although the values here were integers, Perl did a good job and returned a rational number for the division. You can easily see it by calling the <tt>WHAT</tt> method:</p>
<pre>say $x.add($y).WHAT; <em># (Int)</em>
say $x.div($y).WHAT; <em># (Rat)</em></pre>
<p>If you have two integers, the result of their division is always of the Rat type. The actual operator, which is triggered in this case, is the one from src/core/Rat.pm:</p>
<pre>multi sub infix:&lt;/&gt;(Int \a, Int \b) {
    DIVIDE_NUMBERS a, b, a, b
}</pre>
<p>The <tt>DIVIDE_NUMBERS</tt> sub returns a <tt>Rat</tt> value.</p>
<h2>Defining a role</h2>
<p>How to modify the <tt>C</tt> class so that it performs integer division? One of the options is to use a parameterised role:</p>
<pre>role R<strong>[::T]</strong> {
    has <strong>T</strong> $.value;
    
    method add($b) {
        <strong>T</strong>.new($.value + $b.value)
    }

    method div($b) {   
        <strong>T</strong>.new($.value / $b.value)
    }
}</pre>
<p>The parameter in square brackets after the role name restricts both the type of the <tt>$.value</tt> attribute and the return type of the methods, which return a new object of the type <tt>T</tt>. Here, in the template of the role, <tt>T</tt> is just a name, which should later be specified when the role is used.</p>
<h2>Using the role</h2>
<p>So, let’s make it integer:</p>
<pre>class N does R<strong>[Int]</strong> {}</pre>
<p>Now the parts of the role that employ the <tt>T</tt> name replace it with Int, so the class is equivalent to the following definition:</p>
<pre>class C {
    has <strong>Int</strong> $.value;
    
    method add($b) {
        <strong>Int</strong>.new($.value + $b.value)
    }

    method div($b) {   
        <strong>Int</strong>.new($.value / $b.value)
    }
}</pre>
<p>The new class operates with integers, and the result of the division is an exact 3:</p>
<pre>class N does R[<strong>Int</strong>] {}

my N $i = N.new(value =&gt; 10);
my N $j = N.new(value =&gt; 3);

say $i.add($j); <em># 13</em>
say $i.div($j); <em># 3</em></pre>
<p>It is also possible to force floating-point values by instructing the role accordingly:</p>
<pre>class F does R[<strong>Num</strong>] {}

my F $x = F.new(value =&gt; 10e0);
my F $y = F.new(value =&gt; 3e0);

say $x.add($y); <em># 13</em>
say $x.div($y); <em># <span class="s1">3.33333333333333</span></em></pre>
<p>Notice that both values, including 13, are of the Num type now, not Int or Rat as it was before:</p>
<pre>say $x.add($y).WHAT; <em># (Num)</em>
say $x.div($y).WHAT; <em># (Num)</em></pre>
	
